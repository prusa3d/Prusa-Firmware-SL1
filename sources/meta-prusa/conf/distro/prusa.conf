DISTRO = "prusa"
DISTRO_NAME = "Prusa GNU/Linux distro for 3D printers."
DISTRO_VERSION := "${@bb.process.run('${TOPDIR}/../version.sh')[0].strip()}"
DISTRO_CODENAME = "prusix"
SDK_VENDOR = "-prusasdk"
SDK_VERSION := "${DISTRO_VERSION}"
MAINTAINER = "Zavorka <roman.beranek@prusa3d.com>"
TARGET_VENDOR = "-prusa"
LCONF_VERSION = "7"
DISTRO_VERSION[vardepsexclude] = "DATE"
SDK_VERSION[vardepsexclude] = "DATE"

PACKAGE_NO_GCONV = "0"
LOCALE_UTF8_ONLY = "1"
LOCALE_UTF8_IS_DEFAULT = "1"
IMAGE_LINGUAS = "en-us"
ENABLE_BINARY_LOCALE_GENERATION = "1"
GLIBC_GENERATE_LOCALES = "en_US.UTF-8"
GLIBC_SPLIT_LC_PACKAGES = "0"

USER_CLASSES = "buildstats"
SDKMACHINE = "x86_64"

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDK_ARCH}-${IMAGE_BASENAME}-${TUNE_PKGARCH}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

DISTRO_FEATURES_append = " \
	alsa ipv6 systemd usbgadget usbhost usrmerge wifi vfat ldconfig \
	zeroconf ext2 opengl smbfs pam ld-is-gold wayland polkit"
DISTRO_FEATURES_remove = " 3g irda nfc ppp bfs cramfs bluetooth bluez5 \
	ipsec x11 sysvinit ldconfig \
"

VOLATILE_LOG_DIR = "no"

# Configure Qt5 flags (GLES is still required for qtdeclarative and qtwebkit)
PACKAGECONFIG_GL_pn-qtbase = "gles2 linuxfb kms eglfs gbm"
#PACKAGECONFIG_FB_pn-qtbase = "directfb"
PACKAGECONFIG_DISTRO_pn-qtbase = "icu"
PACKAGECONFIG_FONTS_pn-qtbase = "fontconfig"
PACKAGECONFIG_DEFAULT_remove_pn-qtbase = "tests widgets"

# compile mesa for swrast_kms support
PACKAGECONFIG_pn-mesa = "gallium gles egl gbm opengl dri"

PACKAGECONFIG_pn-directfb = "drmkms"

# remove python3 dependencies
PACKAGECONFIG_remove_pn-gpgme = "python3"
PACKAGECONFIG_remove_pn-libbytesize = "python3"
PACKAGECONFIG_remove_pn-volume-key = "python3"
PACKAGECONFIG_remove_pn-opkg-utils_class-target = "python"
PACKAGECONFIG_remove_pn-boost = "python"
PACKAGECONFIG_remove_pn-libxml2 = "python"
PACKAGECONFIG_remove_pn-libxml2-native = "python"

# remove systemd-networkd
PACKAGECONFIG_remove_pn-systemd = "networkd"

EXTRA_OECONF_remove_pn-libsdl = "--disable-video-dummy"
EXTRA_OECONF_append_pn-libsdl = " --enable-video-dummy"
EXTRA_OECONF_append_pn-libsdl2 = " --enable-video-kmsdrm"

TCLIBCAPPEND = ""

LICENSE_FLAGS_WHITELIST = "commercial"
OELAYOUT_ABI = "12"

DISTRO_FEATURES_BACKFILL_CONSIDERED_append = " sysvinit ldconfig"

VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = ""
VIRTUAL-RUNTIME_dev_manager = "systemd"
VIRTUAL-RUNTIME_login_manager = "shadow-base"

VIRTUAL-RUNTIME_base-utils-syslog = ""
VIRTUAL-RUNTIME_base-utils-hwclock = "util-linux-hwclock"
VIRTUAL-RUNTIME_base-utils = "packagegroup-prusa-base-utils"
PREFERRED_PROVIDER_base-utils = "packagegroup-prusa-base-utils"

require conf/distro/include/yocto-uninative.inc
INHERIT += "uninative buildstats package_ipk package_tar sstate license remove-libtool"

SDK_INCLUDE_TOOLCHAIN = "1"
SDK_EXT_TYPE = "full"

INSANE_SKIP_qtmultimedia-plugins += "file-rdeps"
INSANE_SKIP_qtbase-plugins += "file-rdeps"
INSANE_SKIP_qtbase += "file-rdeps"
INSANE_SKIP_nemo-qml-plugin-dbus-qt5 += "file-rdeps"

