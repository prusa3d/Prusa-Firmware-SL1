DISTRO = "prusa"
DISTRO_NAME = "Prusa GNU/Linux distro for 3D printers."
DISTRO_VERSION := "${@bb.process.run('${TOPDIR}/../version.sh')[0].strip()}"
DISTRO_CODENAME = "prusix"
SDK_VENDOR = "-prusasdk"
SDK_VERSION := "${DISTRO_VERSION}"
MAINTAINER = "Zavorka <roman.beranek@prusa3d.com>"
TARGET_VENDOR = "-prusa"
LCONF_VERSION = "7"
DISTRO_VERSION[vardepsexclude] = "DATE"
SDK_VERSION[vardepsexclude] = "DATE"

PACKAGE_NO_GCONV = "0"
LOCALE_UTF8_ONLY = "1"
LOCALE_UTF8_IS_DEFAULT = "1"
IMAGE_LINGUAS = "en-us"
ENABLE_BINARY_LOCALE_GENERATION = "1"
GLIBC_GENERATE_LOCALES = "en_US.UTF-8"
GLIBC_SPLIT_LC_PACKAGES = "0"

USER_CLASSES = "buildstats"
SDKMACHINE = "x86_64"

# Copy the license manifest(but not the license files) to the target.
# see meta/recipes-core/base-files/base-files_3.0.14.bb
COPY_LIC_MANIFEST = "1"
# don't copy the whole directory structure containing license files
COPY_LIC_DIRS = "0"

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDK_ARCH}-${IMAGE_BASENAME}-${TUNE_PKGARCH}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

DISTRO_FEATURES:append = " \
	alsa ipv6 systemd usbgadget usbhost usrmerge wifi vfat ldconfig \
	ext2 opengl smbfs pam ld-is-gold wayland polkit gles2 egl"
DISTRO_FEATURES:remove = " 3g irda nfc ppp bfs cramfs bluetooth bluez5 \
	ipsec x11 sysvinit ldconfig zeroconf \
"

GO_DYNLINK:class-target = ""

VOLATILE_LOG_DIR = "no"

# RRECOMMENDED by openssh-sshd
PACKAGE_EXCLUDE:append = " rng-tools"
PACKAGECONFIG:remove:pn-openssh = "rng-tools"

# Configure Qt5 flags (GLES is still required for qtdeclarative and qtwebkit)
PACKAGECONFIG_GL:pn-qtbase = "gles2 linuxfb kms eglfs gbm"
#PACKAGECONFIG_FB:pn-qtbase = "directfb"
PACKAGECONFIG_DISTRO:pn-qtbase = "icu"
PACKAGECONFIG_FONTS:pn-qtbase = "fontconfig"
PACKAGECONFIG_DEFAULT:remove:pn-qtbase = "tests widgets"
PACKAGECONFIG:append:pn-qtbase = " gif xkbcommon"
PACKAGECONFIG:append:pn-qtmultimedia = " gstreamer"

PACKAGECONFIG:append:pn-gstreamer1.0-plugins-good = " qt5"
PACKAGECONFIG:append:pn-gstreamer1.0-plugins-bad = " modplug"
PACKAGECONFIG:append:pn-gstreamer1.0-plugins-ugly = " a52dec"
PACKAGECONFIG:remove:pn-gstreamer1.0-plugins-good = " cairo flac gdk-pixbuf gudev jpeg lame libpng mpg123 soup speex taglib v4l2"
PACKAGECONFIG:remove:pn-gstreamer1.0-plugins-base = " jpeg ogg pango png theora vorbis"

INSANE_SKIP:gstreamer1.0-plugins-base += "file-rdeps"
INSANE_SKIP:libgstgl-1.0 += "file-rdeps"
INSANE_SKIP:gstreamer1.0-plugins-base-opengl += "file-rdeps"


PACKAGECONFIG:pn-directfb = "drmkms"

# remove systemd-networkd; add resolved for mDNS
PACKAGECONFIG:remove:pn-systemd = "networkd"
PACKAGECONFIG:append:pn-systemd = " resolved"

EXTRA_OECONF:remove:pn-libsdl = "--disable-video-dummy"
EXTRA_OECONF:append:pn-libsdl = " --enable-video-dummy"
EXTRA_OECONF:append:pn-libsdl2 = " --enable-video-kmsdrm"

TCLIBCAPPEND = ""

LICENSE_FLAGS_WHITELIST = "commercial"
OELAYOUT_ABI = "12"

DISTRO_FEATURES_BACKFILL_CONSIDERED:append = " sysvinit ldconfig"

VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = ""
VIRTUAL-RUNTIME_dev_manager = "systemd"
VIRTUAL-RUNTIME_login_manager = "shadow-base"

VIRTUAL-RUNTIME_base-utils-syslog = ""
VIRTUAL-RUNTIME_base-utils-hwclock = "util-linux-hwclock"
VIRTUAL-RUNTIME_base-utils = "packagegroup-prusa-base-utils"
PREFERRED_PROVIDER_base-utils = "packagegroup-prusa-base-utils"

require conf/distro/include/yocto-uninative.inc
INHERIT += "uninative buildstats package_ipk package_tar sstate license remove-libtool"

SDK_INCLUDE_TOOLCHAIN = "1"
SDK_EXT_TYPE = "full"

INSANE_SKIP:qtmultimedia-plugins += "file-rdeps"
INSANE_SKIP:qtbase-plugins += "file-rdeps"
INSANE_SKIP:qtbase += "file-rdeps"
INSANE_SKIP:nemo-qml-plugin-dbus-qt5 += "file-rdeps"

EXTRA_OECONF:append:pn-qemu = " --enable-capstone=git --disable-git-update"
EXTRA_OECONF:append:pn-qemu-native = " --enable-capstone=git --disable-git-update"
EXTRA_OECONF:append:pn-qemu-helper-native = " --enable-capstone=git --disable-git-update"
EXTRA_OECONF:append:pn-qemu-system-native = " --enable-capstone=git --disable-git-update"
